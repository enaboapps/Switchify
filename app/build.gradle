plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.enaboapps.switchify"
        minSdk 31
        targetSdk 34
        versionCode gitVersionCode()
        versionName "1.9.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "FLEKSY_API_KEY", "\"${properties.getProperty('fleksy.apiKey')}\""
        buildConfigField "String", "FLEKSY_API_SECRET", "\"${properties.getProperty('fleksy.apiSecret')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.enaboapps.switchify'
}


// Function to get the version code from the number of commits
def gitVersionCode() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    } catch (Exception e) {
        // Fallback in case the git command fails or is not available
        return 1
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.3'
    implementation "androidx.compose.runtime:runtime-livedata:1.6.8"
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation(platform("com.google.firebase:firebase-bom:33.1.1"))
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "co.thingthing.fleksylib:fleksylib-release:1.2.0"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.6.8"
}